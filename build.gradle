import org.gradle.api.artifacts.*

apply plugin: 'base' // To add "clean" task to the root project.
apply from: 'configuration.gradle'

subprojects {
    apply from: rootProject.file('common.gradle')
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '1.11'
}

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }

task setupDecompWorkspaceAll(dependsOn:
	  projects164.collect { ':' + it + ":setupDecompWorkspace" }
	+ projects172.collect { ':' + it + ":setupDecompWorkspace" }
	+ projects1710.collect { ':' + it + ":setupDecompWorkspace" }) {}

task assembleMC1710(type: Jar, dependsOn: [\
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects1710.collect { ':' + it + ":build" }) {
	archiveName="MineTweaker3-1.7.10-${mineTweakerVersion}.jar"
	
	from zipTree(project(":ZenScript").jar.archivePath)
	from zipTree(project(':MineTweaker3-API').jar.archivePath)
	
	for (p in projects1710) {
		from zipTree(project(p).jar.archivePath)
	}
}

task assembleMC172(type: Jar, dependsOn: [\
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects172.collect { ':' + it + ":build" }) {
	archiveName="MineTweaker3-1.7.2-${mineTweakerVersion}.jar"
	
	from zipTree(project(":ZenScript").jar.archivePath)
	from zipTree(project(':MineTweaker3-API').jar.archivePath)
	
	for (p in projects172) {
		from zipTree(project(p).jar.archivePath)
	}
}

task assembleMC164(type: Jar, dependsOn: [\
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects164.collect { ':' + it + ":build" }) {
	archiveName="MineTweaker3-1.6.4-${mineTweakerVersion}.jar"
	
	from zipTree(project(":ZenScript").jar.archivePath)
	from zipTree(project(':MineTweaker3-API').jar.archivePath)
	
	for (p in projects164) {
		from zipTree(project(p).jar.archivePath)
	}
}

task assembleAll(dependsOn: [
	assembleMC164,
	assembleMC172,
	assembleMC1710]) {
}

task zendoc164(type: Javadoc) {
	source = files([
		'MineTweaker3-API/src/main/java',
		'MineTweaker3-MC164-Main/src/main/java']
		+ projects164.collect { it + '/src/main/java' })
	
	destinationDir = file("build/zendoc164")
	options.docletpath = [file('buildSrc/build/libs/buildSrc.jar')].asList()
	options.doclet = 'stanhebben.zenscript.docs.ZenScriptDoclet'
	
	def classpathList = [
		project(":ZenScript").jar.archivePath,
		project(':MineTweaker3-MC164-Main').jar.archivePath
	]
	
	for (p in projects164) {
		for (file in project(':' + p).configurations.compile) {
			classpathList += file
		}
	}
	
	options.classpath = classpathList.asList()
	options.addStringOption("baseurl", "/doc/mc164/")
}
