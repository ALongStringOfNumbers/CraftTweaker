import org.gradle.api.artifacts.*

apply plugin: 'base' // To add "clean" task to the root project.
apply from: 'configuration.gradle'

subprojects {
    apply from: rootProject.file('common.gradle')
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '2.6'
}

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }

task setupDecompWorkspaceAll(dependsOn: projects1710.collect { ':' + it + ":setupDecompWorkspace" } + projects18.collect { ':' + it + ':setupDecompWorkspace'}) {}

task assembleMC1710(type: Jar, dependsOn: [\
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects1710.collect { ':' + it + ":build" }) {
	archiveName="CraftTweaker-1.7.10-${mineTweakerVersion}.jar"
	
	from zipTree(project(":ZenScript").jar.archivePath)
	from zipTree(project(':MineTweaker3-API').jar.archivePath)
	
	for (p in projects1710) {
		from zipTree(project(p).jar.archivePath)
	}
}

task assembleMC18(type: Jar, dependsOn: [\
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects18.collect { ':' + it + ":build" }) {
	archiveName="CraftTweaker-1.8.8-${mineTweakerVersion}.jar"
	
	from zipTree(project(":ZenScript").jar.archivePath)
	from zipTree(project(':MineTweaker3-API').jar.archivePath)
	
	for (p in projects18) {
		from zipTree(project(p).jar.archivePath)
	}
}

task assembleDevBase1710(type: Jar, dependsOn: [
		':ZenScript:build',
		':MineTweaker3-API:build',
		':MineTweaker3-MC1710-Main:build']) {
	archiveName="CraftTweaker-Dev-1.7.10-${mineTweakerVersion}.jar"
	
	from project(':ZenScript').sourceSets.main.output
	from project(':MineTweaker3-API').sourceSets.main.output
	from project(':MineTweaker3-MC1710-Main').sourceSets.main.output
}

task assembleDevBase18(type: Jar, dependsOn: [
		':ZenScript:build',
		':MineTweaker3-API:build',
		':MineTweaker3-MC18-Main:build']) {
	archiveName="CraftTweaker-Dev-1.8.8-${mineTweakerVersion}.jar"
	
	from project(':ZenScript').sourceSets.main.output
	from project(':MineTweaker3-API').sourceSets.main.output
	from project(':MineTweaker3-MC18-Main').sourceSets.main.output
}

task assembleDevFull1710(type: Jar, dependsOn: [
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects1710.collect { ':' + it + ":build" }) {
	archiveName="CraftTweaker-Dev-Full-1.7.10-${mineTweakerVersion}.jar"
	
	from project(':ZenScript').sourceSets.main.output
	from project(':MineTweaker3-API').sourceSets.main.output
	
	for (p in projects1710) {
		from project(p).sourceSets.main.output
	}
}

task assembleDevFull18(type: Jar, dependsOn: [
		':ZenScript:build',
		':MineTweaker3-API:build']
		+ projects18.collect { ':' + it + ":build" }) {
	archiveName="CraftTweaker-Dev-Full-1.8.8-${mineTweakerVersion}.jar"
	
	from project(':ZenScript').sourceSets.main.output
	from project(':MineTweaker3-API').sourceSets.main.output
	
	for (p in projects18) {
		from project(p).sourceSets.main.output
	}
}

task assembleAll(dependsOn: [
	assembleMC1710,
	assembleMC18,
	assembleDevBase1710,
	assembleDevBase18,
	assembleDevFull1710,
	assembleDevFull18]) {
	
}

task zendoc1710(type: Javadoc) {
	source = files([
		'MineTweaker3-API/src/main/java',
		'MineTweaker3-MC1710-Main/src/main/java']
		+ projects1710.collect { it + '/src/main/java' })
	
	destinationDir = file("build/zendoc1710")
	options.docletpath = [file('buildSrc/build/libs/buildSrc.jar')].asList()
	options.doclet = 'stanhebben.zenscript.docs.ZenScriptDoclet'
	
	def classpathList = [
		project(":ZenScript").jar.archivePath,
		project(':MineTweaker3-MC1710-Main').jar.archivePath
	]
	
	for (p in projects1710) {
		for (file in project(':' + p).configurations.compile) {
			classpathList += file
		}
	}
	
	options.classpath = classpathList.asList()
	options.addStringOption("baseurl", "/doc/mc1710/")
}
